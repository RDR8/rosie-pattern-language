---- -*- Mode: rpl; -*-                                                                             
---- vim:syn=rosie
----
---- rpl-1.1.rpl     RPL language version 1.1, written in RPL version 1.0
----
---- Â© Copyright IBM Corporation 2016, 2017.
---- LICENSE: MIT License (https://opensource.org/licenses/mit-license.html)
---- AUTHOR: Jamie A. Jennings

-- TODO:
-- (0) Enable charset operations (see 'op' below)
-- (1) Add to rosie tests the parsing of the following extensions:
--   repetition with a single parameter, e.g. e{5}
--   optional semi-colons between statements, incl. empty statement
--   nested charset_exps, e.g. [^[[[a-z]][^0-9]]] 
--   set intersection, e.g. [[a-z]&&[xyz]]
--   set difference, e.g. [[a-z]-[xyz]]
-- (2) Add stubs to compiler for these exps, with informative error messages

rpl 1.0

package rpl_1_1

alias newline = [\n]

alias comment = { "--" {!newline .}* {newline / $} }
alias ignore = { { [[:space:]] / comment }* }

alias id_char = { [[:alnum:]] / [_] }
alias id = { [[:alpha:]] id_char* }

localname = id 
          / [.]		 -- a single dot
	  / [$]		 -- a single dollar sign
	  / [~]		 -- a single tilde
packagename = id
identifier = { {packagename "."}? localname }

alias dquote = { [\"] }
alias esc =    { [\\] }
literal = { {!{esc/dquote} .} / {esc .} }* 
alias quoted_string = { dquote literal dquote }


star =     { [*] }
question = { [?] }
plus =     { [+] }
alias end_token = { "end" !id_char }
alias keyword_list = "local" / "alias" / "grammar" / "end"
keyword = { keyword_list !id_char }

-- it is important that an error consume at least one character here.
-- as a tactic for continuing to parse after an error , we will skip to eol and try to continue. 
rest_of_line = { {!newline .}+ ignore }
syntax_error = { rest_of_line }

complement = "^"
character = { {!esc !"[" !"^" !"]" .} / {esc .} }
range = { complement? character [-] !"]" character } 
charlist = { complement? {!"]" character}+ }		    -- cannot be empty
name = {!":" .}+
named_charset = { complement? name } 
alias one_charset = { "[" { ":" named_charset ":]" } /
	                  { range "]" } /
		          { charlist "]" } /
		          syntax_error
		    }
-- grammar
--    charset_exp = { "[" complement? ignore {charset_exp ignore {op ignore}?}+ "]" } /
-- 		 one_charset
--    op = intersection / difference
--    intersection = "&&"
--    difference = "-"
-- end
grammar
   charset_exp = { "[" complement? ignore charset_exp1 ignore "]" } / one_charset
   alias charset_exp1 = charset_combiner / charset_exp 
   charset_combiner = { ignore charset_exp ignore op ignore charset_exp1 }
   op = intersection / difference / union
   union = ""
   intersection = "&&"
   difference = "-"
end

low =  [[0-9]]*				    -- an int or nothing
high = [[0-9]]* 
repetition = { "{" ignore low ignore {"," ignore high ignore}? "}" }
	          
int = ignore [0-9]+

alias quantifier = star / question / plus / repetition

assignment_prefix = identifier ignore "="
stmnt_prefix = {keyword / assignment_prefix}

alias slash =         { [/] }
alias open =          { [(] }
alias openraw =       { [{] }
alias close =         { [)] }
alias closeraw =      { [}] }

negation = { [!] }
lookat = { [@] }
alias predicate_symbol = { negation / lookat }
	    
grammar
   alias exp = {ignore { syntax_error /
	                 application /
	                 choice /
	                 sequence /
	                 quantified_exp / 
	                 plain_exp} }
   syntax_error = { stmnt_prefix rest_of_line }
   application = { identifier ":" } ignore (arg / arglist / rawarglist )
   arglist = { ignore open int_or_exp { ignore "," int_or_exp }* close }
   rawarglist = { ignore openraw int_or_exp { ignore "," int_or_exp }* closeraw }
   arg = quantified_exp / plain_exp / int
   alias int_or_exp = ignore int / exp
   choice = { {quantified_exp / plain_exp} ignore slash exp }
   sequence = { {quantified_exp / plain_exp} {ignore !stmnt_prefix exp}+ }
   alias plain_exp = { ignore 
		       { identifier / quoted_string / raw / cooked / charset_exp / predicate } }
   quantified_exp = { plain_exp ignore quantifier }
   cooked = { open exp+ ignore close }
   raw = { openraw exp+ ignore closeraw }
   predicate = { predicate_symbol ignore {quantified_exp / plain_exp} }
end

grammar
   alias stmnt = local_ / exported
   alias exported = alias_ / grammar_ / assignment_
   local_ = "local" ignore alias_ / grammar_ / assignment_ 
   alias_ = "alias" ignore identifier ignore "=" exp 
   grammar_ = "grammar" { ignore { alias_ / assignment_ } }+ ignore end_token
   assignment_ = identifier ignore "=" exp 
end

importpath = quoted_string		  -- double quoted, interpreted, pattern name is "literal"
              / {[[:alnum:]]/[_-//]}+	  -- whitespace terminated, uninterpreted

major = [0-9]{1,2}
minor = major
version_spec = { major "." minor ignore }
dot = "."
import_spec = ignore importpath ("as" (packagename / dot))?

----------------------------------------------------------------------------------------
-- Top level patterns
----------------------------------------------------------------------------------------

language_decl = ignore {"rpl" [:space:]} version_spec [;]?
package_decl = ignore {"package" [:space:]} packagename [;]?
import_decl = ignore {"import" [:space:]} import_spec ("," ignore import_spec)* [;]?

rpl_statements = (ignore [;] / package_decl / import_decl / stmnt / syntax_error)* ignore $
rpl_expression = exp / syntax_error

preparse = language_decl

-- RPL module structure:
-- rpl_module = language_decl? package_decl import_decl* statement* ignore

---------------------------------------------------------------------------------------------------
-- Tests
---------------------------------------------------------------------------------------------------

-- test language_decl rejects "rpl", "rpl ", "rpl a", "rpl 1.a", "rpl 1.", "rpl 1.123", "rpl 1.\n1"
-- test language_decl accepts "rpl 0.0", "  rpl 1.0\n", "rpl 52.99", "rpl 99.99", "rpl 1.1 -- Comment\n"

-- test package_decl accepts "package foo", "package fooBar", "package f_oo"
-- test package_decl rejects "package", "package _foo", "package 6foo", "package .foo", "package foo.bar"

-- test import_decl rejects "import"

-- importpath can be QUOTED STRING:
-- test import_decl rejects "import \"foo/bar\","
-- test import_decl accepts "import \"foo/bar\""
-- test import_decl accepts "import \"foo/bar\", \"/usr/local/baz\", \"/usr/bin/time\""
-- test import_decl accepts "import \"foo/bar\" as foo"
-- test import_decl accepts "import \"foo/bar\" as foo, \"/usr/local/baz\", \"/usr/bin/time\""
-- test import_decl accepts "import \"foo/bar\" as foo, \"/usr/local/baz\" as ., \"/usr/bin/time\""

-- importpath can be PLAIN SYMBOL, uninterpreted:
-- test import_decl rejects "import foo,"
-- test import_decl rejects "import foo["
-- test import_decl accepts "import foo/bar/baz"
-- test import_decl accepts "import foo/bar", /usr/lo-cal/b_az, \"/usr/bin/time\""
-- test import_decl accepts "import foo/bar as foo"
-- test import_decl accepts "import foo/bar as foo, \"/usr/local/baz\", /usr/bin/time"
-- test import_decl accepts "import -f_o-o as foo, /usr/local/baz as ., /usr/bin/time"
-- test import_decl accepts "import -f_o-o/bar as foo, \"/usr/local/baz\" as ., \"/usr/bin/time\""

-- test rpl_expression accepts "foo_1:x", "pkg.f:x", "f:\"hello\"", "f:!x", "f:x+", "f:(x y z)", "f:{x y z}"
-- test rpl_expression accepts "f:(x, y)", "foo:{a, b}"
-- test rpl_expression accepts "f:(a, 55)", "foo.bar:(1, 2, 3, a b c, d / e, {f}*)"
-- test rpl_expression rejects "f:", "pkg.f:", "f:{a / b, 3}+"
-- test rpl_expression includes arglist "f:(x, y)", "f:(3)"
-- test rpl_expression excludes arglist "f:(x y)"
-- test rpl_expression includes arg "f:(x)", f:(x y)", "f:x", "f:5", "f:{x y}", "f:{x}",
-- test rpl_expression includes arg "f:{a / b}+", "f:{(a)}", "f:{[:set:]}"
-- test rpl_expression includes rawarglist "f:{5}", "f:{7, x, a / b / c+}", "f:{(a),b}", "f:{[:set:]{1,2},\"hi\"}"



