-- -*- Mode: rpl; -*-                                                                               
--
-- ucd.rpl
--
-- Â© Copyright Jamie A. Jennings 2017, 2018.
-- LICENSE: MIT License (https://opensource.org/licenses/mit-license.html)
-- AUTHOR: Jamie A. Jennings

alias digit =	[0-9]
alias hexDigit = [[0-9][A-F]]
alias alphaNum = [[0-9][A-Z][a-z]]
alias digits = digit+
alias label = alphaNum+
alias positiveDecimal = {digits "." digits}
alias decimal = {"-"? positiveDecimal}
alias rational = {"-"? digits {"/" digits}?}
alias optionalDecimal = {"-"? digits {"." digits}?}
alias medial_hyphen = " -" / "- " 
name = {label { {medial_hyphen / [\-_ ]} label}* }
name2 = {label {[\-_ ] label}*}
annotatedName = {name2 {" (" [^)]* ")"}?}	-- name with optional parenthetical annotation
shortName = [A-Z]{0,3}
codePoint = {"10" hexDigit{4}} / hexDigit{4,5}
alias codePoints = { codePoint {" " codePoint}* } -- space-delimited list of 1 to n code points

-- test label accepts "A", "Syriac", "NGKWAEN", "123467", "A005A"
-- test decimal accepts "3.1", "3.5", "-0.5"
-- test rational accepts "3/4", "-3/4"
-- test optionalDecimal accepts "3.5", "-0.5", "2", "1000"
-- test annotatedName accepts "NEXT LINE (NEL)", "CARRIAGE RETURN (CR)"
-- test shortName accepts "", "O", "WA", "WAE"
-- test codePoint accepts "00A0", "E0100", "10FFFF"


-- N.B. We define boundary to be ";" (consuming space on either side), or looking at "#" (start of a
-- comment), or the end of line.
alias ~ = {[ \t]* {";"/>"#"} [ \t]*} / $ 
alias skipfield = [^;]*
comment = { [ \t]* "#" .* }
blank_line = [ \t]*$


-- -----------------------------------------------------------------------------
-- UnicodeData.txt
-- -----------------------------------------------------------------------------

first_last = label
defined_range_name = { name2 {" " name2}* }
defined_range = { "<" defined_range_name "," " "* first_last " "* ">" }

-- test defined_range accepts "<CJK Ideograph Extension A, First>"
-- test defined_range accepts "<CJK Ideograph Extension A, Last>"


character_name = defined_range / skipfield
gc = name
upper = codePoint?
lower = codePoint?
title = codePoint?

UnicodeData = codePoint				-- (0) Codepoint in hex
	      character_name			-- (1) Name
	      gc				-- (2) General Category
	      (skipfield){9}			-- (3) Canonical Combining Class
                                                -- (4) Bidi Class
						-- (5) Decomposition Type and Mapping
      						-- (6,7,8) Numeric Type and Value
						-- (9) Bidi Mirrored
						-- (10,11) Obsolete
	      upper				-- (12) Simple Uppercase Mapping
	      lower				-- (13) Simple Lowercase Mapping
	      title				-- (14) Simple Titlecase Mapping

UnicodeData_line = comment / UnicodeData

-- Sometimes the name field indicates the start/end of a range like these:
--   test UnicodeData_line accepts "3400;<CJK Ideograph Extension A, First>;Lo;0;L;;;;;N;;;;;"
--   test UnicodeData_line accepts "4DB5;<CJK Ideograph Extension A, Last>;Lo;0;L;;;;;N;;;;;"
-- And these are the typical lines:
--   test UnicodeData_line accepts "0000;<control>;Cc;0;BN;;;;;N;NULL;;;;"
--   test UnicodeData_line accepts "0001;<control>;Cc;0;BN;;;;;N;START OF HEADING;;;;"
--   test UnicodeData_line accepts "0030;DIGIT ZERO;Nd;0;EN;;0;0;0;N;;;;;"
--   test UnicodeData_line accepts "0031;DIGIT ONE;Nd;0;EN;;1;1;1;N;;;;;"
--   test UnicodeData_line accepts "0032;DIGIT TWO;Nd;0;EN;;2;2;2;N;;;;;"
--   test UnicodeData_line accepts "0041;LATIN CAPITAL LETTER A;Lu;0;L;;;;;N;;;;0061;"
--   test UnicodeData_line accepts "0042;LATIN CAPITAL LETTER B;Lu;0;L;;;;;N;;;;0062;"


-- -----------------------------------------------------------------------------
-- Enumeration and Category files (including Binary properties)
-- -----------------------------------------------------------------------------

codePointRange = { codePoint ".." codePoint }
propertyName = name2

EnumeratedProp = (codePointRange / codePoint) propertyName comment? .*

EnumeratedProp_line = comment / EnumeratedProp / blank_line

-- test EnumeratedProp_line accepts "#  All code points not explicitly listed for Script"
-- test EnumeratedProp_line accepts "#  have the value Unknown (Zzzz)."
-- test EnumeratedProp_line accepts ""
-- test EnumeratedProp_line accepts "0000..001F    ; Common # Cc  [32] <control-0000>..<control-001F>"
-- test EnumeratedProp_line accepts "0020          ; Common # Zs       SPACE"
-- test EnumeratedProp_line accepts "0021..0023    ; Common # Po   [3] EXCLAMATION MARK..NUMBER SIGN"
-- test EnumeratedProp_line accepts "0024          ; Common # Sc       DOLLAR SIGN"


-- -----------------------------------------------------------------------------
-- Multi-property files (name; value)
-- -----------------------------------------------------------------------------

propertyValue = codePoints / name

MultiProp = (codePointRange / codePoint) propertyName propertyValue? comment? .*

MultiProp_line = comment / MultiProp / blank_line

-- test MultiProp_line accepts "#  All code points not explicitly listed for Script"
-- test MultiProp_line accepts "#  have the value Unknown (Zzzz)."
-- test MultiProp_line accepts ""
-- test MultiProp_line accepts "03D2  ; FC_NFKC; 03C5           # L&  GREEK UPSILON WITH HOOK SYMBOL"
-- test MultiProp_line accepts "03D3  ; FC_NFKC; 03CD           # L&  GREEK UPSILON WITH ACUTE AND HOOK SYMBOL"
-- test MultiProp_line accepts "33C6  ; FC_NFKC; 0063 2215 006B 0067 #So SQUARE C OVER KG"
-- test MultiProp_line accepts "33C7  ; FC_NFKC; 0063 006F 002E # So  SQUARE CO"
-- test MultiProp_line accepts "2ADC          ; Expands_On_NFD # Sm       FORKING"



